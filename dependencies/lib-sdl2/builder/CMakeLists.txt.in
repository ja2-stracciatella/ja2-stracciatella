# \file dependencies/lib-fltk/builder/CMakeLists.txt.in
# Configured by @CMAKE_CURRENT_LIST_FILE@
#
# @ONLY variables:
#  * CMAKE_GENERATOR - which generator to use
#  * CACHE_FILE - initial cache of the external project
#  * INSTALL_PREFIX - where to install the files generated by the external project

cmake_minimum_required(VERSION 3.1)

project(builder NONE)

include(ExternalProject)
externalproject_add(sdl2
    URL "https://libsdl.org/release/SDL2-2.0.20.zip"
    URL_MD5 "e371cd64e8dd12407c9ecf9861e4874f"
    CMAKE_ARGS
        "-G@CMAKE_GENERATOR@"
        "-C@CACHE_FILE@"
        "-DCMAKE_INSTALL_PREFIX=@INSTALL_PREFIX@"
        "-DCMAKE_SYSTEM_VERSION=@CMAKE_SYSTEM_VERSION@"
        "-DCMAKE_TOOLCHAIN_FILE=@CMAKE_TOOLCHAIN_FILE@"
        "-DANDROID_ABI=@ANDROID_ABI@"
        "-DANDROID_APP_PLATFORM=@ANDROID_APP_PLATFORM@"
        -DCMAKE_BUILD_TYPE=RelWithDebInfo
        -DSDL_SHARED=ON
    BUILD_COMMAND ${CMAKE_COMMAND} --build . --config RelWithDebInfo
    # This patch is for android 2.0.20 compatibility with NDK r25, otherwise SDL2 will not compile
    # It can be removed when upgrading to 2.24.0. See https://github.com/libsdl-org/SDL/issues/6019
    PATCH_COMMAND patch -p1 < ${CMAKE_CURRENT_SOURCE_DIR}/ndk25.patch
    INSTALL_COMMAND ${CMAKE_COMMAND} --build . --config RelWithDebInfo --target install
    BUILD_ALWAYS 1
)
