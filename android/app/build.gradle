apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 30
    ndkVersion "25.0.8775105"

    lintOptions {
        disable 'NewApi'
        disable 'ExpiredTargetSdkVersion'
    }

    defaultConfig {
        applicationId "io.github.ja2stracciatella"
        minSdkVersion 19
        targetSdkVersion 26

        // Version name is the semver version
        String version = file('../../version').text.trim()
        logger.info('Stracciatella Version Name: "{}".', version)
        // Version code is build from semver
        // pad each semver part to 3 characters -> join -> parse as int
        // Example: 0.17.1 -> [ "000", "017", "001" ] -> "000017001" -> 17001
        Integer versionCodeFromVersion = version.split('\\.').collect({ it.padLeft(3, '0') }).join('').toInteger()
        logger.info('Stracciatella Version Code: "{}".', versionCodeFromVersion)

        versionName version
        versionCode versionCodeFromVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                 "-std=c++17"
                arguments '-DBUILD_SDL_LIB=ON',
                          '-DBUILD_LAUNCHER=OFF',
                          '-DWITH_UNITTESTS=OFF',
                          '-DWITH_RUST_BINARIES=OFF',
                          '-DCMAKE_CXX_FLAGS=-fPIC',
                          '-DCMAKE_C_FLAGS=-fPIC'
            }
        }

        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            // Currently we dont have armv7 as there is an issue with the SDL version we use
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    signingConfigs {
        release {
            storeFile file("${System.getenv("HOME")}/.stracciatella-android-signing-keys/keystore.jks")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias "stracciatella-signing-key"
            keyPassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            assets {
                srcDirs = [ "../../assets" ]
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0-RC2"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.github.noelchew:storage-chooser:2.0.4.4a'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}

tasks.register("projectVersion") {
    doLast {
        println(android.defaultConfig.versionName)
    }
}
