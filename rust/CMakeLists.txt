cmake_minimum_required(VERSION 2.8)

find_package(Cargo REQUIRED)

set(LIBSTRACCIATELLA_BUILD_SWITCHES "")
set(LIBSTRACCIATELLA_BUILD_TYPE "debug")
set(LIBSTRACCIATELLA_BUILD_DIR "${CMAKE_BINARY_DIR}/rust")
set_directory_properties(PROPERTIES ADDITIONAL_MAKE_CLEAN_FILES ${LIBSTRACCIATELLA_BUILD_DIR})

if(CMAKE_BUILD_TYPE STREQUAL "Release")
    set(LIBSTRACCIATELLA_BUILD_TYPE "release")
    set(LIBSTRACCIATELLA_BUILD_SWITCHES ${LIBSTRACCIATELLA_BUILD_SWITCHES} "--${LIBSTRACCIATELLA_BUILD_TYPE}")
endif()

set(LIBRARY_LOCATION "${LIBSTRACCIATELLA_BUILD_DIR}/${LIBSTRACCIATELLA_BUILD_TYPE}/")
if (MSVC)
    set(LIBRARY_LOCATION "${LIBRARY_LOCATION}deps/")
endif()
set(LIBRARY_LOCATION "${LIBRARY_LOCATION}${CMAKE_SHARED_LIBRARY_PREFIX}stracciatella${CMAKE_SHARED_LIBRARY_SUFFIX}")

FILE(TO_NATIVE_PATH ${LIBSTRACCIATELLA_BUILD_DIR} NATIVE_BUILD_DIR)
FILE(TO_NATIVE_PATH ${LIBRARY_LOCATION} NATIVE_LIBRARY_LOCATION)
add_custom_command(
    OUTPUT ${LIBRARY_LOCATION}
    COMMAND ${CMAKE_COMMAND} -E env RUST_BACKTRACE=1 CARGO_TARGET_DIR=${NATIVE_BUILD_DIR} ${CARGO_EXECUTABLE} build --verbose ${LIBSTRACCIATELLA_BUILD_SWITCHES}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)
add_custom_target(rust-build DEPENDS ${LIBRARY_LOCATION})

add_custom_target(rust-unit-tests
    COMMAND ${CMAKE_COMMAND} -E env CARGO_TARGET_DIR=${NATIVE_BUILD_DIR} ${CARGO_EXECUTABLE} test --verbose ${LIBSTRACCIATELLA_BUILD_SWITCHES}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

add_library(rust-stracciatella SHARED IMPORTED GLOBAL)
set_property(TARGET rust-stracciatella PROPERTY IMPORTED_LOCATION ${NATIVE_LIBRARY_LOCATION})
set_property(TARGET rust-stracciatella PROPERTY IMPORTED_IMPLIB ${NATIVE_LIBRARY_LOCATION}${CMAKE_IMPORT_LIBRARY_SUFFIX})
set_property(TARGET rust-stracciatella PROPERTY IMPORTED_NO_SONAME "1")
add_dependencies(rust-stracciatella rust-build)
