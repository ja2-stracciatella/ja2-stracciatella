cmake_minimum_required(VERSION 3.1)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/../cmake")

find_package(Cargo 0.9.0 REQUIRED)
find_package(Rustc 1.15.0 REQUIRED)

set(LIBSTRACCIATELLA_BUILD_SWITCHES "")
set(LIBSTRACCIATELLA_BUILD_TYPE "debug")
set(LIBSTRACCIATELLA_BUILD_DIR "${CMAKE_BINARY_DIR}/rust")
set_directory_properties(PROPERTIES ADDITIONAL_MAKE_CLEAN_FILES ${LIBSTRACCIATELLA_BUILD_DIR})

if(CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo" OR CMAKE_BUILD_TYPE STREQUAL "Release")
    set(LIBSTRACCIATELLA_BUILD_TYPE "release")
    set(LIBSTRACCIATELLA_BUILD_SWITCHES ${LIBSTRACCIATELLA_BUILD_SWITCHES} "--${LIBSTRACCIATELLA_BUILD_TYPE}")
endif()

set(TARGET_DIR "${LIBSTRACCIATELLA_BUILD_DIR}/${LIBSTRACCIATELLA_BUILD_TYPE}/")
if (MSVC)
    set(TARGET_DIR "${TARGET_DIR}deps/")
endif()

add_custom_target(rust-build ALL
    COMMAND ${CMAKE_COMMAND} -E env RUST_BACKTRACE=1 CARGO_TARGET_DIR=${LIBSTRACCIATELLA_BUILD_DIR} ${CARGO_EXECUTABLE} build ${LIBSTRACCIATELLA_BUILD_SWITCHES}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

install(DIRECTORY ${TARGET_DIR} DESTINATION . FILES_MATCHING PATTERN "*.a")
